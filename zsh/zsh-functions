##################################################
## locate: ${XDG_CONFIG_HOME}/zsh/zsh-functions ##
## detail: configuration file for zsh functions ##
##################################################

# use history, but skip certain cmds/args from being written:
function zshaddhistory {
  local line cmd
  line=${1%%$'\n'} ; cmd=${line%% *}
  [[ ${#line} -ge 5
      && ${cmd} != (history|fc|[zx]edit|[zx]load)
  ]] && (( ! $argv[(I)mpq *] ))
}

# safely recompile and reload shell settings:
function zload {
  source /etc/profile &>/dev/null
  autoload -U zrecompile
  [[ -f ~/.zshrc ]] && zrecompile -p ~/.zshrc
  [[ -f ~/.zcompdump ]] && zrecompile -p ~/.zcompdump
  [[ -f ~/.zshrc.zwc.old ]] && rm -f ~/.zshrc.zwc.old
  [[ -f ~/.zcompdump.zwc.old ]] && rm -f ~/.zcompdump.zwc.old
  source ~/.zshrc
}

# go up Nth amount of directories:
# a bit quicker than ../../.. (better zsh alternatives?)
function up {
  local arg=${1:-1};
  while [ ${arg} -gt 0 ]; do
    cd .. >& -;
    arg=$((${arg} - 1));
  done
}

# one-liners/micro functions:
function dropcache { sync && print 1 | doas tee /proc/sys/vm/drop_caches }
function mkcd { command mkdir -p "${@}" && cd "$@" }
function pqiv { command pqiv -t "${@}" 2>/dev/null }
function startx { [[ ${+DISPLAY} -eq 1 ]] || { cd && /usr/bin/xinit ${XDG_CONFIG_DIR:-$HOME}/xorg/xinitrc -auth ${XDG_CONFIG_DIR:-$HOME}/xorg/.serverauth.${RANDOM[1,4]} -nolisten tcp -once -retro -- vt7 &>!${XDG_CONFIR_DIR:-$HOME}/xorg/log } }
